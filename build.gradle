plugins {
    id 'java-library'
    id 'eu.xenit.amp' version '1.0.1'
    id 'eu.xenit.alfresco' version '1.0.1'
    id 'eu.xenit.enterprise-conventions.oss' version '0.1.0'
    id "be.vbgn.ci-detect" version '0.5.0'
    id "org.sonarqube" version "3.1.1"
    id "maven-publish"
    id "signing"
}

description = 'Arender amp for Alfresco'
group = 'eu.xenit.transformers'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom(implementation)
    integrationTestImplementation.extendsFrom(alfrescoProvided)
}

def baseVersion = '1.0.2'
def isRelease = ci.isCi() && ci.branch.startsWith("release")
if (isRelease)
    version = "$baseVersion"
else
    version = "$baseVersion-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    alfrescoPublic()
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test, group: "verification") {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    onlyIf { project.hasProperty("arender.address") }
    doFirst {
        systemProperty("arender.address", project.property("arender.address"))
    }
}

check.dependsOn(integrationTest);

pluginManager.withPlugin("idea") {
    idea {
        module {
            testSourceDirs += sourceSets.integrationTest.java.sourceDirectories
            testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        }
    }
}

dependencies {
    alfrescoProvided("org.alfresco:alfresco-repository:5.1.e") {
        exclude group: "org.springframework.social"
    }

    integrationTestImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    integrationTestRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')
}

publishing {
    publications {
        all {
            pom {
                url = 'https://github.com/xenit-eu/Arender4Alfresco'
                name = 'Arender4Alfresco'
                description = "This project aims to allow alfresco to connect and use a remote arender server "

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/Arender4Alfresco.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/Arender4Alfresco.git'
                    url = 'https://github.com/xenit-eu/Arender4Alfresco.git'
                }

                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
            }
        }
        mavenJava(MavenPublication) {
            from components.java
            artifact amp
        }
    }
    if (project.hasProperty('sonatypeUsername')) {
        repositories {
            if (version.endsWith("-SNAPSHOT")) {
                sonatypeSnapshots {
                    credentials {
                        username project.property('sonatypeUsername')
                        password project.property('sonatypePassword')
                    }
                }
            } else {
                sonatypeMavenCentral {
                    credentials {
                        username project.property('sonatypeUsername')
                        password project.property('sonatypePassword')
                    }
                }
            }
        }
    }
}
